// `streq` tests for equality of the two input strings.

// int ptr int ptr -- int
fn streq in

  // early return if lengths not equal
  peek str1c _ str2c _ in
    str1c str2c != if false ret end
  end

  let _ str1v count str2v in
    0 1 - // -1u64
    while 1 + dup count < do
      dup dup str1v + @ swap str2v + @ != if
        // we found some bytes not equal!
        drop false ret
      end
    end drop
  end

  true
end

fn main in
  "test1" "test2" streq if "1st EQUAL\n" puts end
  "test"  "test"  streq if "2nd EQUAL\n" puts end
  "abcde" "abcde" streq if "3rd EQUAL\n" puts end
  "abcdf" "abcde" streq if "4th EQUAL\n" puts end
end
